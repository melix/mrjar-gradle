def compat = { it.replaceFirst(/^.*\./, '') }

if (project.hasProperty('crossCompile')) {
   // this will configure the java compile tasks with the appropriate JDK
   project.afterEvaluate {
      tasks.withType(JavaCompile) {
          def jdkVersion = compat(sourceCompatibility)
          def jdkHome = System.getenv("JAVA_${jdkVersion}")
          if (!jdkHome) {
             println "Warning: Please set path to JDK ${jdkVersion} using environment variable JAVA_${jdkVersion}"
             println "Falling back to current JDK"
          } else {
             options.fork = true
             options.forkOptions.javaHome = file(jdkHome)
             doFirst {
                 println "$name compiles using JDK $jdkVersion"
             }
          }
      }
   }
} else {
   if (!JavaVersion.current().java9Compatible) {
       throw new GradleException("You must use -PcrossCompile to enable cross compilation, or run Gradle with JDK 9")
   }
   project.afterEvaluate {
      tasks.withType(JavaCompile) {
         def jdkVersion = compat(sourceCompatibility)
         println "Configuring $name to use --release $jdkVersion"
         options.compilerArgs.addAll(['--release', jdkVersion])
      }
   }
}
